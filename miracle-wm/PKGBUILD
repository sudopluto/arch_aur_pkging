# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Pranav Sharma <pranav.sharma.ama@gmail.com>

pkgname=miracle-wm
pkgver=0.4.1
pkgrel=1
pkgdesc="Wayland compositor based on mir"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/miracle-wm-org/miracle-wm"
license=('GPL-3.0-or-later')
depends=('mir' 'libnotify')
makedepends=('cmake' 'yaml-cpp' 'nlohmann-json' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('250bfe04f3f887f64bb7dfa98c9d6f56827ca628b68851424a20b5783ba13482')

build() {
  local _flags=(
    -B build 
    -S "${pkgname}-${pkgver}" 
    -GNinja
    -Wno-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    # miracle-wm options
    -DSYSTEMD_INTEGRATION=ON
  )

  cmake "${_flags[@]}"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
